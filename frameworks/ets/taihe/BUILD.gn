#
# Copyright (c) 2025-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//base/telephony/call_manager/callmanager.gni")

copy_taihe_idl("copy_telephonycall") {
  sources = [ "idl/ohos.telephony.call.taihe" ]
}

subsystem_name = "telephony"
part_name = "call_manager"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_telephonycall" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.telephony.call.ani.cpp",
    "$taihe_generated_file_path/src/ohos.telephony.call.abi.c",
  ]
}

taihe_shared_library("telephony_call_taihe_native") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [
    "${call_manager_path}/frameworks/ets/taihe/include",
    "${call_manager_path}/interfaces/innerkits",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.telephony.call.impl.cpp",
    "src/taihe_call_ability_callback.cpp",
    "src/taihe_call_manager_callback.cpp",
    "src/taihe_call_manager.cpp",
    "src/util.cpp",
    "src/taihe_ani_callback_common.cpp",
    "src/taihe_ani_call_update_async_result.cpp",
  ]
  deps = [
    ":run_taihe",
    "//base/telephony/call_manager:tel_call_manager",
    "//base/telephony/call_manager/frameworks/native:tel_call_manager_api",
  ]
  external_deps = [
    "ability_base:base",
    "ability_base:want",
    "ability_runtime:ability_manager",
    "ability_runtime:ani_common",
    "c_utils:utils",
    "core_service:libtel_common",
    "core_service:tel_core_service_api",
    "hilog:libhilog",
    "image_framework:image",
    "image_framework:image_native",
    "init:libbegetutil",
    "libuv:uv",
    "napi:ace_napi",
    "runtime_core:ani_helpers",
    "runtime_core:libarkruntime",
  ]

  defines = [
    "TELEPHONY_LOG_TAG = \"CallManagerEtsApi\"",
    "LOG_DOMAIN = 0xD001F10",
  ]

  sanitize = {
    cfi = false
    cfi_cross_dso = false
    debug = false
  }
  branch_protector_ret = "pac_ret"
}

generate_static_abc("telephony_call_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.telephony.call.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/telephony_call_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("telephony_call_etc") {
  source = "$target_out_dir/telephony_call_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":telephony_call_abc" ]
}
group("telephony_call_taihe") {
  deps = [
    ":telephony_call_etc",
    ":telephony_call_taihe_native",
  ]
}